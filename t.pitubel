*BASIC
NEW
AUTO
REM>PiTubeList
:
DIM code% &100
target%=&2000
scratch=&70
OSBYTE=&FFF4
OSASCI=&FFE3
:
FOR pass%=0 TO 1
P%=target%
O%=code%
[ OPT 4+3*pass%
LDA #0
LDX #1
JSR OSBYTE
CPX #0
BNE notelectron
LDA #ASC"4"
STA textfxnum+1
LDA #ASC"7"
STA textfxnum+2
.notelectron
LDA #text MOD 256
STA scratch
LDA #text DIV 256
STA scratch+1
LDY #0
.loop
LDA (scratch),Y
BEQ exit
JSR OSASCI
INC &70
BNE loop
INC &71
JMP loop
.exit
RTS
.text
EQUS " n Processor - *FX "
.textfxnum
EQUS "151,230,n"
EQUB &0D
.textend
EQUB 0 \ to stop test
]
size%=O%-code%
NEXT
:
PRINT "Code assembled at &";~code%;"+";~size%;" target &";~target%
END
:
DEFPROCdumpcode
LOCAL linebytes%,lineaddr,coladdr%,byte%
linebytes%=12
:
REM dump the code part
FOR lineaddr%=target% TO text-1 STEP linebytes%
PRINT "  ";
FOR coladdr%=lineaddr% TO FNmin(coladdr%+linebytes%-1,text-1)
IF coladdr%>lineaddr% THEN PRINT " ";
byte%=?(coladdr%-target%+code%)
PRINT "0x";FNchex(byte%);",";
NEXT
PRINT
NEXT
:
REM dump the text part
FOR lineaddr%=text TO textend-1 STEP linebytes%
PRINT "  ";
FOR coladdr%=lineaddr% TO FNmin(coladdr%+linebytes%-1,textend-1)
IF coladdr%>lineaddr% THEN PRINT " ";
byte%=?(coladdr%-target%+code%)
IF byte%>=32 AND byte%<=126 THEN PRINT "'";CHR$(byte%);"', "; ELSE PRINT "0x"+FNchex(byte%)+",";
NEXT
PRINT
NEXT
ENDPROC
:
DEFFNmin(a%,b%)
IF a%<b% THEN =a%
=b%
:
REM print byte in C hex format
DEFFNchex(byte%)
=FNlower(RIGHT$(STR$~(byte% OR &100),2))
:
DEFFNlower(s$)
LOCAL n%,r$,c$
r$=""
FOR n%=1 TO LEN(s$)
c$=MID$(s$,n%,1)
IF c$>="A" AND c$<="Z" THEN c$=CHR$(ASC(c$) OR 32)
r$=r$+c$
NEXT
=r$
